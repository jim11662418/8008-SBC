Name 8008SBC_1;
Partno G22V10#1;
Revision;
Date 09/17/2021;
Designer Jim Loos;
Company ;
Location ;
Assembly 8008 SBC;
Device G22V10;

/* Inputs: */
Pin 1 = S0;
Pin 2 = S1;
Pin 3 = S2;
Pin 4 = SYNC;
Pin 5 = CC1;
Pin 6 = CC2;
Pin 7 = FF_CLK;
Pin 8 = FF_Q;  
Pin 9 = FF_QNOT;

/* Outputs:   */
Pin 23 = ST3;
Pin 22 = ALL;
Pin 21 = ALH;
Pin 20 = MEMRD;
Pin 19 = MEMWR;
Pin 18 = IORD;
Pin 17 = IOWR;
Pin 16 = PHASE1;
Pin 15 = PHASE2;
Pin 14 = DBD;

/* states */
t1      = !S2 &  S1 & !S0;
t1i     =  S2 &  S1 & !S0;
t2      =  S2 & !S1 & !S0;
wait    = !S2 & !S1 & !S0;
t3      = !S1 & !S1 &  S0;
stopped = !S2 &  S1 &  S0;
t4      =  S2 &  S1 &  S0;
t5      =  S2 & !S1 &  S0;

/* cycle types... */
pci = !CC1 & !CC2;            /* Memory Read Instruction */
pcr = !CC1 &  CC2;            /* Memory Read Additional */
pcc =  CC1 & !CC2;            /* I/O Operation */
pcw =  CC1 &  CC2;            /* Memory Write */

/* 2 phase clock */
PHASE1 = !FF_CLK & !FF_Q;                       /* Phase 1 clock to 8008 pin 15         */
PHASE2 = !FF_CLK & !FF_QNOT;                    /* Phase 2 clock to 8008 pin 16         */

/* address and data bus */
DBD = (t1 # t1i # t2 # t3) & !SYNC;             /* 74LS245 Data Bus Direction control   */
ALL  = (t1 # t1i) & !SYNC & PHASE2;             /* 74LS273 #1 Address Latch Low         */
ALH  = t2 & !SYNC & PHASE2;                     /* 74LS273 #2 Address Latch High        */

/* memory and I/O strobes */
!MEMRD = t3 & (pci # pcr) & SYNC;               /* Memory Read strobe to 27256 and 6264 */
!MEMWR = t3 & pcw & !SYNC & PHASE2;             /* Memory Write strobe to 6264          */

!IORD  = t3 & pcc & SYNC;                       /* IO Read to G22V10 #2                 */
!IOWR = t3 & pcc & !SYNC & PHASE1;              /* IO Write to G22V10 #2                */

ST3 = t3 & PHASE2 & !SYNC;                      /* clock interrupt control flip-flop    */ 
